dnl
dnl Configure for librdf
dnl
dnl (Process this file with autoconf to produce a configure script.)

AC_REVISION($Revision$)dnl

AC_PREREQ(2.13)
AC_INIT(rdf_node.c)
AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE(librdf, 0.1)

AM_MAINTAINER_MODE

dnl Checks for programs.
AC_CANONICAL_HOST
AC_ARG_PROGRAM
AM_SANITY_CHECK
AM_PROG_CC_STDC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB
dnl AM_PROG_LIBTOOL

AM_MISSING_PROG(ACLOCAL, aclocal, $missing_dir)
AM_MISSING_PROG(AUTOCONF, autoconf, $missing_dir)
AM_MISSING_PROG(AUTOMAKE, automake, $missing_dir)
AM_MISSING_PROG(AUTOHEADER, autoheader, $missing_dir)




dnl Checks for libraries.
dnl GNU GDBM
AC_CHECK_HEADERS(gdbm.h)
if test "$ac_cv_header_gdbm_h" = yes ; then
  AC_CHECK_LIB(gdbm,gdbm_firstkey)
fi

dnl Berkeley DB
AC_CHECK_HEADERS(db.h)
if test "$ac_cv_header_db_h" = yes ; then
  AC_CHECK_LIB(db, db_create)
  AC_CHECK_LIB(db, db_open)
  if test "$ac_cv_lib_db_db_create" = yes; then
    AC_DEFINE(HAVE_DB_CREATE)
  fi
  if test "$ac_cv_lib_db_db_open" = yes; then
    AC_DEFINE(HAVE_DB_OPEN)
  fi
  AC_MSG_CHECKING(whether DB_TXN defined in db.h)
  dnl In BDB, DB_TXN is a pointer to a structure never defined
  AC_TRY_LINK([#include <db.h>], [DB_TXN* ptr=(DB_TXN*)NULL],
              AC_DEFINE(HAVE_BDB_DB_TXN)
	      AC_MSG_RESULT(yes),
	      AC_MSG_RESULT(no))
  AC_MSG_CHECKING(whether DBC defined in db.h)
  AC_TRY_LINK([#include <db.h>], [size_t len=sizeof(DBC)],
              AC_DEFINE(HAVE_BDB_CURSOR)
	      AC_MSG_RESULT(yes),
	      AC_MSG_RESULT(no))
fi



dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(stdlib.h unistd.h string.h fcntl.h)


dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_C_BIGENDIAN

AC_CHECK_TYPE(byte, MISSING)
AC_CHECK_TYPE(u16, MISSING)
AC_CHECK_TYPE(u32, MISSING)

AC_CHECK_SIZEOF(unsigned char, 1)
AC_CHECK_SIZEOF(unsigned short, 2)
AC_CHECK_SIZEOF(unsigned int, 4)
AC_CHECK_SIZEOF(unsigned long, 4)
AC_CHECK_SIZEOF(unsigned long long, 8)

if test "$ac_cv_sizeof_unsigned_short" = "0" \
   || test "$ac_cv_sizeof_unsigned_int" = "0" \
   || test "$ac_cv_sizeof_unsigned_long" = "0"; then
    AC_MSG_WARN([Hmmm, something is wrong with the sizes - using defaults]);
fi


dnl Checks for library functions.
AC_FUNC_MEMCMP

dnl Checks for URI resolvers
AC_ARG_WITH(libwww, [  --with-libww  Use libwww [yes]], enable_libwww="$withval", enable_libwww="no") 

AC_MSG_CHECKING(whether to use libwww)
if test "$enable_libwww" = yes ; then
  AC_MSG_RESULT(yes)
  AC_CHECK_PROG(LIBWWW_CONFIG, libwww-config, yes, no)
  if test "$LIBWWW_CONFIG" = yes; then
    LIBS="`libwww-config --libs` $LIBS"
    CFLAGS="`libwww-config --cflags` $CFLAGS"
    AC_DEFINE(HAVE_LIBWWW)
  fi
else
  AC_MSG_RESULT(no)
fi


dnl Checks for modules
digest_modules="md5 sha1 ripemd160"

AC_MSG_CHECKING(digests wanted)
AC_ARG_ENABLE(digests, [  --enable-digests=LIST   Use digests [md5 sha1 ripemd160]], digest_modules="$enableval") 
AC_MSG_RESULT($digest_modules)

DIGEST_OBJS=
DIGEST_SRCS=

AC_ARG_WITH(openssl-digests, [  --with-openssl-digests  Use openssl digests [yes]], enable_openssl_digests="$withval", enable_openssl_digests="yes") 

# This is needed because autoheader can't work out which computed
# symbols must be pulled from acconfig.h into config.h.in
if test "x" = "y"; then
  AC_DEFINE(HAVE_OPENSSL_CRYPTO_MD5_DIGEST)
  AC_DEFINE(HAVE_OPENSSL_CRYPTO_SHA1_DIGEST)
  AC_DEFINE(HAVE_OPENSSL_CRYPTO_RIPEMD160_DIGEST)
fi

AC_MSG_CHECKING(whether to use openssl digests)
if test "$enable_openssl_digests" = yes ; then
  AC_MSG_RESULT(yes)
  AC_CHECK_HEADERS(openssl/crypto.h)
  if test "$ac_cv_header_openssl_crypto_h" = yes ; then
    AC_DEFINE(HAVE_OPENSSL_DIGESTS)
    new_digest_modules=
    oLIBS="$LIBS"
    LIBS="-lcrypto $LIBS"
    have_libcrypto=no

    for module in $digest_modules; do
      func=`echo $module | tr 'abcdefghijklmnopqrstuvwxyz' 'ABCDEFGHIJKLMNOPQRSTUVWXYZ'`
      found=
      AC_MSG_CHECKING(for openssl $func digest module)
      AC_CACHE_VAL(ac_cv_lib_crypto_$func,
                   [AC_TRY_LINK(, $func(),
                   [eval "ac_cv_lib_crypto_$func=yes"],
                   [eval "ac_cv_lib_crypto_$func=no"])])
      if eval "test \"`echo '$ac_cv_lib_crypto_'$func`\" = yes"; then
	AC_MSG_RESULT(yes)
	n=HAVE_OPENSSL_CRYPTO_${func}_DIGEST
	AC_DEFINE_UNQUOTED($n)
        have_libcrypto=yes
      else
	AC_MSG_RESULT(no)
	new_digest_modules="${new_digest_modules} $module"
      fi
    done
    if test "$have_libcrypto" = no; then
      LIBS="$oLIBS"
    fi
    DIGEST_OBJS="$DIGEST_OBJS rdf_digest_openssl.o"
    DIGEST_SRCS="$DIGEST_SRCS rdf_digest_openssl.c"
    digest_modules=$new_digest_modules
  fi
else
  AC_MSG_RESULT(no)
fi


# This is needed because autoheader can't work out which computed
# symbols must be pulled from acconfig.h into config.h.in
if test "x" = "y"; then
  AC_DEFINE(HAVE_LOCAL_MD5_DIGEST)
  AC_DEFINE(HAVE_LOCAL_SHA1_DIGEST)
  AC_DEFINE(HAVE_LOCAL_RIPEMD160_DIGEST)
fi

# Maybe add some local digest modules
for module in $digest_modules; do
  module_u=`echo $module | tr 'abcdefghijklmnopqrstuvwxyz' 'ABCDEFGHIJKLMNOPQRSTUVWXYZ'`
  AC_MSG_CHECKING(for local $module digest module)
  if test -r $srcdir/$module.c; then
    AC_MSG_RESULT(yes);

    n=HAVE_LOCAL_${module_u}_DIGEST
    AC_DEFINE_UNQUOTED($n)

    DIGEST_OBJS="$DIGEST_OBJS $module.o"
    DIGEST_SRCS="$DIGEST_SRCS $module.c"
  else
    AC_MSG_RESULT(no);
  fi
done

AC_SUBST(DIGEST_OBJS)
AC_SUBST(DIGEST_SRCS)


HASH_OBJS=
HASH_SRCS=
AC_MSG_CHECKING(for libgdbm support)
if test "$ac_cv_lib_gdbm_gdbm_firstkey" = yes ; then
  AC_MSG_RESULT(yes);
  AC_DEFINE(HAVE_GDBM_HASH)
  HASH_OBJS="$HASH_OBJS rdf_hash_gdbm.o"
  HASH_SRCS="$HASH_SRCS rdf_hash_gdbm.o"
else
  AC_MSG_RESULT(no);
fi

AC_MSG_CHECKING(for libdb support)
if test "$ac_cv_lib_db_db_open" = yes ; then
  AC_MSG_RESULT(yes);
  AC_DEFINE(HAVE_BDB_HASH)
  HASH_OBJS="$HASH_OBJS rdf_hash_bdb.o"
  HASH_SRCS="$HASH_SRCS rdf_hash_bdb.o"
else
  AC_MSG_RESULT(no);
fi


AC_SUBST(HASH_OBJS)
AC_SUBST(HASH_SRCS)


dnl Checks for packages
SD=docs

#if test -d libxml2-2.1.1; then 
#  SD="$SD libxml2-2.1.1"
#  (cd libxml2-2.1.1 && ./configure --cache=../config.cache --enable-shared=no)
#fi

AC_SUBST(SD)

AC_OUTPUT(Makefile
docs/Makefile
librdf-config,
[chmod +x librdf-config])
